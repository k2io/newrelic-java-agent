import com.nr.builder.publish.PublishConfig

plugins {
    id("maven-publish")
    id("signing")
}

apply from: '../gradle/script/spotbugs-config.gradle'
apply from: '../gradle/script/jacoco.gradle'

jar {
    from("$rootDir/LICENSE")
    manifest {
        attributes 'Implementation-Title': 'New Relic Bridge DataStore', 'Implementation-Version': project.version
    }
}

tasks.withType(GenerateModuleMetadata.class) {
    enabled = false
}


tasks.withType(Javadoc).configureEach {
    javadocTool = javaToolchains.javadocToolFor {
        languageVersion = JavaLanguageVersion.of(11)
    }

    options.addBooleanOption("-frames", true)
    options.addBooleanOption("-no-module-directories", true)
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    implementation(project(":newrelic-api"))
    implementation(project(":newrelic-weaver-api"))
    implementation(project(":agent-bridge"))

    testImplementation('org.awaitility:awaitility:4.2.0')
    testImplementation(fileTree(dir: 'src/test/resources/com/newrelic/agent/bridge', include: '*.jar'))
}

PublishConfig.config(
        project,
        "New Relic Java agent bridge datastore API",
        "The bridge datastore API of the Java agent.") { it ->
    it.from(components.java)
}
